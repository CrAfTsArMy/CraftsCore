plugins {
    id 'java'
    id 'maven-publish'
}

group 'de.craftsblock'
version '3.8.6'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    group = "${rootProject.group}.${rootProject.name.toLowerCase()}"
    version = rootProject.version

    publishing {
        publications {
            normal(MavenPublication) {
                artifactId project.name.toLowerCase()

                from components.java

                pom {
                    name = "CraftsCore ${project.name}"
                    description = 'A simple Lib that simplifies some things'
                    url = 'https://github.com/CrAfTsArMy/CraftsCore'

                    scm {
                        url = 'https://github.com/CrAfTsArMy/CraftsCore'
                        connection = 'scm:git:git://github.com/CrAfTsArMy/CraftsCore.git'
                        developerConnection = 'scm:git:git@github.com:CrAfTsArMy/CraftsCore.git'
                    }

                    issueManagement {
                        system = 'github'
                        url = 'https://github.com/CrAfTsArMy/CraftsCore/issues'
                    }

                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://github.com/CrAfTsArMy/CraftsCore/blob/master/LICENSE'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url('https://repo.craftsblock.de/releases')
                authentication {
                    basic(BasicAuthentication) {
                    }
                }
                credentials {
                    username = project.findProperty("craftsblockRepoUsername")
                    password = project.findProperty("craftsblockRepoPassword")
                }
            }
        }
    }
}
