group 'de.craftsblock'
version '3.8.6'

subprojects {
    apply plugin: 'maven-publish'

    group = "${rootProject.group}.${rootProject.name.toLowerCase()}"
    version = rootProject.version

    afterEvaluate {
        publishing {
            publications {
                normal(MavenPublication) {
                    artifactId project.name.toLowerCase()

                    def component = project.components.findByName('java') ?: project.components.findByName('javaPlatform')
                    if (component == null) {
                        logger.warn("${project.name} does not contain a component that is publishable!")
                        return
                    }

                    from component

                    pom {
                        name = "CraftsCore ${project.name}"
                        description = 'A simple Lib that simplifies some things'
                        url = 'https://github.com/CrAfTsArMy/CraftsCore'

                        scm {
                            url = 'https://github.com/CrAfTsArMy/CraftsCore'
                            connection = 'scm:git:git://github.com/CrAfTsArMy/CraftsCore.git'
                            developerConnection = 'scm:git:git@github.com:CrAfTsArMy/CraftsCore.git'
                        }

                        issueManagement {
                            system = 'github'
                            url = 'https://github.com/CrAfTsArMy/CraftsCore/issues'
                        }

                        licenses {
                            license {
                                name = 'Apache License 2.0'
                                url = 'https://github.com/CrAfTsArMy/CraftsCore/blob/master/LICENSE'
                            }
                        }
                    }
                }
            }
            repositories {
                maven {
                    url('https://repo.craftsblock.de/releases')
                    authentication {
                        basic(BasicAuthentication) {
                        }
                    }
                    credentials {
                        username = project.findProperty("craftsblockRepoUsername")
                        password = project.findProperty("craftsblockRepoPassword")
                    }
                }
            }
        }
    }
}
