apply plugin: 'java-library'

repositories {
    mavenCentral()
}

gradle.projectsEvaluated {

    def validSubprojects = rootProject.subprojects.findAll { sub ->
        sub.name != project.name &&
                (!sub.ext.has("shouldPublish") || sub.ext["shouldPublish"]) &&
                sub.tasks.findByName("classes") != null
    }

    java {
        def version = validSubprojects
                .collect { it.java.sourceCompatibility as String }
                .findAll { it != null }
                .collect { JavaVersion.toVersion(it) }
                .max() ?: JavaVersion.VERSION_1_8

        sourceCompatibility = version
        targetCompatibility = version
    }

    dependencies {
        validSubprojects.sort { "${it.name}" }.each { sub ->
            api project(":${sub.name}")
        }
    }

}